philosopher left_fork right_fork done {
    let name { (self) }
    let milliseconds { 10 }

    (printf " [ %p ] waiting for left fork\n" name)
    (receive left_fork)
    (sleep milliseconds)

    (printf " [ %p ] waiting for right fork\n" name)
    (receive right_fork)
    (sleep milliseconds)

    (printf " [ %p ] dining\n" name)
    (sleep milliseconds)

    (printf " [ %p ] returning left fork\n" name)
    (send left_fork 0)
    (sleep milliseconds)

    (printf " [ %p ] returning right fork\n" name)
    (send right_fork 0)
    (sleep milliseconds)

    (printf " [ %p ] thinking\n" name)
    (send done 0)
}

iter c n {
    if (= n 0) {
        0
    } else {
        (receive c)
        (iter c (- n 1))
    }
}

main {
    let fork0 { (channel) }
    let fork1 { (channel) }
    let fork2 { (channel) }
    let fork3 { (channel) }
    let fork4 { (channel) }

    (send fork0 0)
    (send fork1 0)
    (send fork2 0)
    (send fork3 0)
    (send fork4 0)

    let done { (channel) }

    (spawn philosopher fork0 fork1 done)
    (spawn philosopher fork1 fork2 done)
    (spawn philosopher fork2 fork3 done)
    (spawn philosopher fork3 fork4 done)

    (spawn philosopher fork0 fork4 done)

    (iter done 5)

    (printf "Done!\n")
}
